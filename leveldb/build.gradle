apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

group 'com.github.hf'
version '1.7.0'

install {
  repositories.mavenInstaller {
    pom.project {
      name 'leveldb'
      description 'LevelDB bindings for Android.'
      url 'https://github.com/hf/leveldb-android'
      inceptionYear '2014'
        
      packaging 'aar'
      groupId 'com.github.hf'
      artifactId 'leveldb'
      
      version '1.7.0'
      
      licenses {
        license {
          name 'BSD 3-Clause'
          url 'https://github.com/hf/leveldb-android/blob/master/LICENSE.txt'
          distribution 'repo'
        }
      }
     
      scm {
        connection 'https://github.com/hf/leveldb-android.git'
        url 'https://github.com/hf/leveldb-android'
      }
      
      developers {
        developer {
          name 'Stojan Dimitrovski'
        }
      }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key  = System.getenv('BINTRAY_KEY')

  configurations = ['archives']

  pkg {
    repo = 'android'
    name = 'leveldb-android'
    licenses = ['BSD 3-Clause']
    vcsUrl = 'https://github.com/hf/leveldb-android.git'
    
    userOrg = user
    
    websiteUrl = 'https://github.com/hf/leveldb-android'
    issueTrackerUrl = 'https://github.com/hf/leveldb-android'
    
    githubRepo = 'hf/leveldb-android'
    githubReleaseNotesFile = 'README.md'
    
    version {
      name = '1.7.0'
      desc = 'LevelDB (1.7) for Android.'
      released = new Date()
      vcsTag = 'leveldb-android-1.7.0'
    }
  }
}

android {
    compileSdkVersion 24 
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 24 
        versionCode 1
        versionName "1.7.0"

        testApplicationId "com.github.hf.leveldb.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        testHandleProfiling true
        testFunctionalTest true
    }
    
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }
}

dependencies {
    androidTestCompile 'commons-io:commons-io:2.+'
    androidTestCompile 'org.assertj:assertj-core:1.6.+'
}

task preBuildLevelDB {

    doLast {
        exec {
            commandLine 'sh', projectDir.getAbsolutePath() + '/src/main/jni-prebuild/prebuild'
        }
    }
}

preBuild.dependsOn preBuildLevelDB
preBuild {}.mustRunAfter preBuildLevelDB
